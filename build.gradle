buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.squareup.sqldelight:gradle-plugin:1.5.4'
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.7.20'
}

apply plugin: 'com.squareup.sqldelight'

group = 'edu.uwaterloo.cs'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url("https://git.uwaterloo.ca/api/v4/projects/69446/packages/maven")
        name "GitLab"
        credentials(HttpHeaderCredentials) {
            name = 'Deploy-Token'
            value = 'KtqpZE3e2fURjpqgd7BG'
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

kotlin {
    def hostOs = System.getProperty("os.name")
    def hostArch = System.getProperty("os.arch")
    def isMingwX64 = hostOs.startsWith("Windows")
    def nativeTarget
    if (hostOs == "Mac OS X")
    {
        if (hostArch == "x86_64")
            nativeTarget = macosX64('native')
        else
            nativeTarget = macosArm64('native')
    }
    else if (hostOs == "Linux") nativeTarget = linuxX64("native")
    else if (isMingwX64) nativeTarget = mingwX64("native")
    else throw new GradleException("Host OS is not supported in Kotlin/Native.")

    nativeTarget.with {
        binaries {
            executable {
                entryPoint = 'main'
            }
        }
    }

    sourceSets {
        nativeMain {
            dependencies {
                implementation("edu.uwaterloo.cs:todo-lib:1.0.0.6-DEV")
                implementation("com.github.ajalt.clikt:clikt:3.5.0")
                implementation("com.github.ajalt.mordant:mordant:2.0.0-beta7")
                implementation("com.squareup.sqldelight:native-driver:1.5.4")
            }
        }
        nativeTest {
            dependencies {
                implementation kotlin('test')
            }
        }
    }
}

sqldelight {
    Database { // This will be the name of the generated database class.
        packageName = "edu.uwaterloo.cs.db"
    }
    linkSqlite = true
}
